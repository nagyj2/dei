Grammar

	/* performs math on integers */
math: 	math '+' math											<< $$ = $1 + $3 >>
	|   	math '-' math											<< $$ = $1 - $3 >>
	|   	math '*' math											<< $$ = $1 * $3 >>
	|   	math '//' math										<< $$ = (int) ($1 / $3) >>
	|   	math '%' math											<< $$ = $1 % $3 >>
	|   	math '^' math											<< $$ = (int) ($1 ** $3) >>
	|   	math '==' math										<< $$ = ($1 == $3) ? 1 : 0 >>
	|   	math '!=' math										<< $$ = ($1 != $3) ? 1 : 0 >>
	|   	math '>' math											<< $$ = ($1  > $3) ? 1 : 0 >>
	|   	math '<' math											<< $$ = ($1  < $3) ? 1 : 0 >>
	|   	math '>=' math										<< $$ = ($1 >= $3) ? 1 : 0 >>
	|   	math '<=' math										<< $$ = ($1 <= $3) ? 1 : 0 >>
	|   	'-' math       		%prec UMINUS		<< $$ = - $1 >>
	|			'(' math ')'											<< $$ = $2 >>
	|   	NUM       												<< $$ = $1 >>
	|   	IDENT															<< $$ = lookup($1) >>
	|   	set                            		<< $$ = S($1) >>

  /* performs set math on roll results */
set:  	set '&' set												<< $$ = set($1 ∩ $3) >>
	|   	set '|' set												<< $$ = set($1 ∪ $3) >>
	|   	set '&&' set											<< $$ = $1 ∩ $3 >>
	|   	set '||' set											<< $$ = $1 ∪ $3 >>
	|   	'[' set ']'												<< $$ = $2 >>
	|   	func															<< $$ = Z($1) >>

	/* performs functions on roll sets */
func: 	die																<< $$ = R($1) >>
	|   	'{' list '}'											<< $$ = Q($1) >>
	|			func F_ADD a_args									<< $$ = F($2, $1, $2) >>
	|			func F_SUB s_args									<< $$ = F($2, $1, $2) >>
	|			func F_MOD m_args									<< $$ = F($2, $1, $2) >>

a_args:	squant ssel fquant 								<< $$ = C($3, $2, $1, NULL) >>
	|			psel fquant												<< $$ = C($2, $1,  1, NULL) >>

s_args:	squant ssel 											<< $$ = C( 1, $2, $1, NULL) >>
	|			psel 															<< $$ = C( 1, $1,  1, NULL) >>

m_args:	squant ssel fquant								<< $$ = C($3, $2, $1, NULL) >>
	|			squant ssel 'if' COND fquant			<< $$ = C($5, $2, $1, $4) >>
	|			psel fquant												<< $$ = C($3, $2, $1, NULL) >>
	|			psel 'if' COND fquant							<< $$ = C($5, $2, $1, $4) >>

	/* single select */
ssel:		SSELECT														<< $$ = $1 >>
	|			NUM																<< $$ = $1 >>

	/* multi select */
psel:		PSELECT														<< $$ = $1 >>
	|			NUM 's'														<< $$ = $1 >>

	/* selection quantifier */
squant: NUM																<< $$ = $1 >>
	|			SQUANT 														<< $$ = $1 >>
	|																				<< $$ =  1 >>

	/* function quantifier */
fquant:	NUM 'times'												<< $$ = $1 >>
	|			FQUANT														<< $$ = $1 >>
	|																				<< $$ =  1 >>

	/* definition of a die with standard faces */
die: 		DNUM 'd' NUM											<< D($1,  1, $3) >>
	|   	DNUM 'd' '{' nnum RANGE nnum '}'	<< D($1, $4, $6) >>
	|   	'd' NUM														<< D( 1,  1, $2) >>
	|   	'd' '{' nnum RANGE nnum '}'				<< D( 1, $3, $5) >>
 	|			DNUM 'd' '{' list '}'							<< d($1, $4) >>
	|   	'd' '{' list '}'									<< d( 1, $3) >>

	/* create a list of integers */
list: 	nnum															<< $$ = val($1, NULL) >>
	|			nnum ',' list											<< $$ = val($1, $3) >>

	/* optionally negative number */
nnum:		NUM																<< $$ = I($1) >>
	|			'-' NUM														<< $$ = M($1,NULL) >>

	/* performs top-level actions */
stmt:		stmt math EOL											<<  >>
	|   	stmt IDENT ':' math EOL						<<  >>
	|   	stmt error EOL										<<  >>
	|   	stmt EOL													<<  >>
	|			'@' stmt math EOL									<<  >>
	|   	'@' stmt IDENT ':' math EOL				<<  >>
	|   	'@' stmt error EOL								<<  >>
	|   	'@' stmt EOL											<<  >>
	|																				<<  >>

	/* Definitions of important terminals */
F_ADD:		append

F_SUB:		drop
	|				count
	|				choose

F_MOD:		reroll

COND:			lower
	|				higher

SSELECT:	highest
	| 			lowest
	| 			random

PSELECT:	unique

FQUANT:		once
	|				twice
	|				thrice

SQUANT:		one
	|				two
	|				three
	|				four
	|				five

OPERATORS 	: + # add roll result
						| - # subtract roll result
						| * # multiply roll result
						| '//' # integer divide roll result
						| % # modulo roll result
						| ^ # exponentiate result
						| & # element-wise intersection of dice set (eliminate duplicates)
						| | # element-wise union of dice set (eliminate duplicates)
						| && # set-wise intersection of dice set (factor duplicates)
						| || # set-wise union of dice set (keep duplicates)
						| =  # equality check roll result ([0] or [1] result)
						| != # inequality check roll result ([0] or [1] result)
						| >  # greater than check roll result ([0] or [1] result)
						| <  # lesser than check roll result ([0] or [1] result)
						| >= # greater than or equal to check roll result ([0] or [1] result)
						| <= # lesser than or equal to roll result ([0] or [1] result)

PRECIDENCE:
	lo	=, !=, >, <, >=, <=
			+, -
			*, '//', %
			^
			&&, ||
	hi	&, |

comments:		block style with '\'
